#!/bin/bash
# Restart with wish: \
[ z`uname -o` == zCygwin ] && exec wish86 $(cygpath -w $0) "$@" || exec tclsh $0 "$@"

package require Tk 8.5
if { [catch {package require Ttk}] } {
	puts "NOTE: Using good ol' Motif style - Tile not found. Not a problem except poor look&feel."
}

set filename [lindex $argv 0]
if { $filename == "" } {
	puts "Usage: [file tail $argv0] <file to rename>"
	exit 1
}

if { ![file exists $filename] } {
	puts "Error: file not found"
	exit 1
}

entry .e
frame .b
button .b.ok -text "Ok" -command do_rename
button .b.cancel -text "Cancel" -command exit

font create DisplayNameFont {*}[font configure TkFixedFont]
font configure DisplayNameFont -size -20
.e configure -font DisplayNameFont

bind .e <Key-Return> do_rename
bind .e <Key-Escape> exit

pack .e .b -fill x
pack .b.ok .b.cancel -side left

.e insert end $filename

wm geometry . "+500+500"
focus .e

proc do_rename {} {
	set new_name [.e get]
	if { $new_name == $::filename } {
		puts "Name not changed."
		exit 0
	}

	set options ""
	if { [file exists $new_name] } {
		if { [tk_dialog .ask "Rename overwrite warning" "File '$new_name' exists, do you want to overwrite it?" "" 0 Skip Overwrite] == 0 } {
			puts "Renaming cancelled"
			exit 0
		}
		set options -force
	}

	if { [catch {file rename {*}$options $::filename $new_name} res] } {
		puts "ERROR: $res"
		exit 1
	}
	puts "Renamed '$::filename' to '$new_name'"
	exit 0
}

# vim: ft=tcl
